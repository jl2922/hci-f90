module dets_module

  use constants_module
  use det_module
  use types_module

  implicit none

  private

  public :: dets_type
  public :: assignment(=)

  type dets_type
    private
    integer, public :: n = 0 ! n <= 2nd rank of up/dn.
    integer :: det_size = 0
    integer(LONG), allocatable :: up(:, :)
    integer(LONG), allocatable :: dn(:, :)
    logical, public :: is_sorted = .true. ! True if sorted in increasing order.
    contains
      procedure, public :: reserve
      procedure, public :: append
      procedure, public :: initialize
      procedure, public :: get_det
      procedure, public :: set_det
      procedure, public :: sort ! Into ascending order.
      procedure, public :: filter_by_sorted_dets
      procedure, public :: merge_sorted_dets
  end type dets_type

  interface assignment(=)
    module procedure assign_dets
  end interface

  contains

  subroutine reserve(this, capacity)
    class(dets_type), intent(inout) :: this
    integer, intent(in) :: capacity
    if (allocated(this%up)) deallocate(this%up)
    if (allocated(this%dn)) deallocate(this%dn)
    allocate(this%up(this%det_size, capacity))
    allocate(this%dn(this%det_size, capacity))
  end subroutine reserve

  subroutine append(this, det)
    class(dets_type), intent(inout) :: this
    class(det_type), intent(in) :: det

    if (size(this%up, 2) == this%n) then
      stop 'Not enough capacity.'
    endif
    this%n = this%n + 1
    call this%set_det(this%n, det)
  end subroutine append

  subroutine initialize(this, n_orb, capacity)
    class(dets_type), intent(inout) :: this
    integer, intent(in) :: n_orb
    integer, intent(in) :: capacity
    this%det_size = ceiling(n_orb * 1.0 / C%TRUNK_SIZE)
    call this%reserve(capacity)
    this%n = 0
    this%up = 0
    this%dn = 0
  end subroutine initialize

  function get_det(this, idx) result(det)
    class(dets_type), intent(inout) :: this
    integer, intent(in) :: idx
    type(det_type) :: det
    
    if (idx > this%n) then
      print *, 'idx/n: ', idx, this%n
      call backtrace
      stop 'get_idx out of bound.'
    end if
    call det%resize(this%det_size)
    det%up%trunks(:) = this%up(:, idx)
    det%dn%trunks(:) = this%dn(:, idx)
  end function get_det

  subroutine set_det(this, idx, det)
    class(dets_type), intent(inout) :: this
    integer, intent(in) :: idx
    type(det_type), intent(in) :: det

    if (idx > this%n) stop 'set_idx out of bound.'
    this%up(:, idx) = det%up%trunks(:)
    this%dn(:, idx) = det%dn%trunks(:)
    if (this%n > 1) then
      this%is_sorted = .false.
    end if
  end subroutine set_det

  subroutine sort(this)
    class(dets_type), intent(inout) :: this
    integer :: i
    integer :: n
    integer(LONG), allocatable :: sorted_up(:, :)
    integer(LONG), allocatable :: sorted_dn(:, :)
    integer, allocatable :: order(:)
    integer, allocatable :: tmp_order(:)

    n = this%n
    if (n == 0) return
    allocate(order(n))
    allocate(tmp_order(n))
    do i = 1, n
      order(i) = i
    enddo

    call recur(1, n)

    allocate(sorted_up(this%det_size, n))
    allocate(sorted_dn(this%det_size, n))
    do i = 1, n
      sorted_up(:, i) = this%up(:, order(i))
      sorted_dn(:, i) = this%dn(:, order(i))
    enddo
    deallocate(this%up)
    deallocate(this%dn)
    call move_alloc(sorted_up, this%up)
    call move_alloc(sorted_dn, this%dn)
    this%is_sorted = .true.

    contains

    recursive subroutine recur(left, right)
      integer, intent(in) :: left, right
      integer :: tmp
      integer :: mid
      integer :: ptr1, ptr2
      integer :: pos

      if (left == right) return
      if (left == right - 1) then
        if (this%get_det(right) < this%get_det(left)) then
          tmp = order(left)
          order(left) = order(right)
          order(right) = tmp
        endif
        return
      endif

      mid = (left + right) / 2
      call recur(left, mid)
      call recur(mid + 1, right)

      tmp_order(left: mid) = order(left: mid)
      ptr1 = left
      ptr2 = mid + 1
      pos = left
      do while (pos < right .and. ptr1 <= mid .and. ptr2 <= right)
        if (this%get_det(tmp_order(ptr1)) < this%get_det(order(ptr2))) then
          order(pos) = tmp_order(ptr1)
          ptr1 = ptr1 + 1
        else
          order(pos) = order(ptr2)
          ptr2 = ptr2 + 1
        endif
        pos = pos + 1
      enddo
      if (ptr1 <= mid) then
        order(pos: right) = tmp_order(ptr1: mid)
      endif
    end subroutine recur

  end subroutine sort

  subroutine filter_by_sorted_dets(this, dets)
    ! Filter out the dets in this sorted array if they exist in the sorted dets passed in.
    ! Assuming no duplication.
    class(dets_type), intent(inout) :: this
    type(dets_type), intent(inout) :: dets
    integer :: n_left_dets
    integer :: dets_idx
    integer :: i
    integer :: n
    integer, allocatable :: left_dets_indices(:)
    integer(LONG), allocatable :: new_up(:, :), new_dn(:, :)

    if (.not. dets%is_sorted) stop 'dets are not sorted.'
    if (.not. this%is_sorted) stop 'target dets are not sorted.'
    
    n = this%n
    if (n == 0) return
    if (dets%n == 0) return
    allocate(left_dets_indices(n))
    n_left_dets = 0
    dets_idx = 1
    do i = 1, n
      if (dets_idx > dets%n) exit
      do while (dets%get_det(dets_idx) < this%get_det(i) .and. dets_idx < dets%n)
        dets_idx = dets_idx + 1
      enddo
      if (dets%get_det(dets_idx) == this%get_det(i)) then
        dets_idx = dets_idx + 1
        cycle
      endif
      n_left_dets = n_left_dets + 1
      left_dets_indices(n_left_dets) = i
    enddo

    allocate(new_up(this%det_size, n_left_dets))
    allocate(new_dn(this%det_size, n_left_dets))
    do i = 1, n_left_dets
      new_up(:, i) = this%up(:, left_dets_indices(i))
      new_dn(:, i) = this%dn(:, left_dets_indices(i))
    enddo
    deallocate(this%up)
    deallocate(this%dn)
    call move_alloc(new_up, this%up)
    call move_alloc(new_dn, this%dn)
    this%n = n_left_dets
  end subroutine filter_by_sorted_dets

  subroutine merge_sorted_dets(this, dets)
    ! Merge two sorted dets array into one sorted dets array.
    ! Assuming no duplication.
    class(dets_type), intent(inout) :: this
    type(dets_type), intent(inout) :: dets
    integer :: n
    integer :: dets_idx
    integer :: n_merged_dets
    integer :: i
    integer :: idx
    integer, allocatable :: merged_dets_indices(:)
    integer :: ptr_this, ptr_dets
    integer(LONG), allocatable :: new_up(:, :), new_dn(:, :)

    if (.not. dets%is_sorted) stop 'dets are not sorted.'
    if (.not. this%is_sorted) stop 'target dets are not sorted.'

    n = this%n
    if (dets%n == 0) return
    if (n == 0) this%det_size = dets%det_size
    allocate(merged_dets_indices(n + dets%n)) ! Indices > n for input dets.
    n_merged_dets = 0
    ptr_this = 1
    ptr_dets = 1
  
    do while (ptr_this <= n .and. ptr_dets <= dets%n)
      n_merged_dets = n_merged_dets + 1
      if (this%get_det(ptr_this) < dets%get_det(ptr_dets)) then
        merged_dets_indices(n_merged_dets) = ptr_this
        ptr_this = ptr_this + 1
      else if (this%get_det(ptr_this) == dets%get_det(ptr_dets)) then
        merged_dets_indices(n_merged_dets) = ptr_this
        ptr_this = ptr_this + 1
        ptr_dets = ptr_dets + 1
      else
        merged_dets_indices(n_merged_dets) = ptr_dets + n
        ptr_dets = ptr_dets + 1
      endif
    enddo
    do while (ptr_this <= n)
      n_merged_dets = n_merged_dets + 1
      merged_dets_indices(n_merged_dets) = ptr_this
      ptr_this = ptr_this + 1
    enddo
    do while (ptr_dets <= dets%n)
      n_merged_dets = n_merged_dets + 1
      merged_dets_indices(n_merged_dets) = ptr_dets + n
      ptr_dets = ptr_dets + 1
    enddo

    allocate(new_up(this%det_size, n_merged_dets))
    allocate(new_dn(this%det_size, n_merged_dets))
    do i = 1, n_merged_dets
      idx = merged_dets_indices(i)
      if (idx <= n) then
        new_up(:, i) = this%up(:, idx)
        new_dn(:, i) = this%dn(:, idx)
      else
        new_up(:, i) = dets%up(:, idx - n)
        new_dn(:, i) = dets%dn(:, idx - n)
      endif
    enddo
    if(allocated(this%up)) deallocate(this%up)
    if(allocated(this%dn)) deallocate(this%dn)
    call move_alloc(new_up, this%up)
    call move_alloc(new_dn, this%dn)
    this%n = n_merged_dets
  end subroutine merge_sorted_dets

  subroutine assign_dets(dest, src)
    class(dets_type), intent(in) :: src
    class(dets_type), intent(out) :: dest

    call dest%initialize(src%det_size, size(src%up, 2))
    dest%n = src%n
    dest%up(:, :) = src%up(:, :)
    dest%dn(:, :) = src%dn(:, :)
  end subroutine assign_dets

end module dets_module
